import org.gradle.api.artifacts.maven.MavenDeployment

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.6
targetCompatibility = 1.6

group = 'com.github.rholder'
version = '1.0.1-SNAPSHOT'
ext.packaging = 'jar'

repositories {
    mavenCentral()
}

configurations {
    configRuntime {
        extendsFrom runtime
    }
}

configurations.compile {
    resolutionStrategy.cacheChangingModulesFor 0, "seconds"
}

sourceSets {
    main {
        compileClasspath += configurations.configRuntime
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task exportJavadoc(type: Copy, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    into 'javadoc'
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

dependencies {
    // junit testing
    testCompile "junit:junit:4.8.2"
    testCompile "org.mockito:mockito-all:1.9.0"
    testCompile "backport-util-concurrent:backport-util-concurrent:3.1"
}

// enable Gradle Wrapper support for bootstrapping with specific version
task wrapper(type: Wrapper) {
    gradleVersion = '1.5'
}

//********* artifact signing *********
if(gradle.startParameter.taskNames.contains("uploadArchives")) {
    signing {
        sign configurations.archives
    }
} else {
    task signArchives {
        // do nothing
    }
}

uploadArchives {
    repositories {
        if (version.contains("SNAPSHOT")) {
            mavenLocal()
        } else {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name 'moar-concurrent'
                    packaging 'jar'
                    description "This module contains a collection of useful builders and concurrency classes to assist in modeling complex or overly tweakable concurrent processing pipelines."
                    url 'http://rholder.github.com'

                    scm {
                        url 'scm:git@github.com:rholder/moar-concurrent.git'
                        connection 'scm:git@github.com:rholder/moar-concurrent.git'
                        developerConnection 'scm:git@github.com:rholder/moar-concurrent.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'rholder'
                            name 'Ray Holder'
                        }
                    }
                }
            }
        }
    }
}

idea {
    module {
        scopes.PROVIDED.plus += configurations.configRuntime
    }
}
